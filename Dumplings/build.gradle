buildscript {
    ext.kotlin_version = '0.8.11'
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
        classpath 'com.googlecode.androidannotations:androidannotations-api:3.0-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

configurations {
    compile
    androidannotations.extendsFrom(compile)
}

dependencies {
//    compile 'junit:junit:4.12-SNAPSHOT'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.googlecode.androidannotations:androidannotations-api:3.0-SNAPSHOT'
    androidannotations 'com.googlecode.androidannotations:androidannotations:3.0-SNAPSHOT'
    compile 'org.hamcrest:hamcrest-library:1.3'
    compile 'org.mockito:mockito-core:1.9.5'
    compile 'com.google.dexmaker:dexmaker-mockito:1.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

android {
    compileSdkVersion 19
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 17
    }

    sourceSets {
        instumentTest.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    signingConfigs {
        release {
            storeFile file("/home/tass/Keystore.jks")
            storePassword "*****"
            keyAlias "Sinclair"
            keyPassword "*****"
        }
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            jniDebuggable false
            signingConfig signingConfigs.release
        }
    }
}

task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    def storePw = new String(System.console().readPassword("\nKeystore password: "))
    def keyPw = new String(System.console().readPassword("\nKey password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}

afterEvaluate  { project ->
    android.applicationVariants.each { variant ->
        variant.javaCompile.options.compilerArgs += [
                '-classpath', configurations.compile.asPath,
                '-processorpath', configurations.androidannotations.asPath,
                '-processor', 'com.googlecode.androidannotations.AndroidAnnotationProcessor',
                '-AandroidManifestFile=' + variant.outputs[0].processResources.manifestFile,
                '-s', "${project.projectDir}/gen/"
        ]
    }
}